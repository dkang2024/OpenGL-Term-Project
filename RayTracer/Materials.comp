// Dealing with seperate structures for materials
const int LAMBERTIAN = 0;
const int REFLECTIVE = 1;
const int DIELECTRIC = 2;
const int LIGHT = 3;

// Lambertian scattering for a material (thanks to https://raytracing.github.io/books/RayTracingInOneWeekend for giving me the math necessary for this)
void lambertianScatter(inout hitRecord record, inout uvec4 state){
    ONB basis = createONB(record.normalVector);
    vec3 scatteredDir = normalize(transformONB(basis, randomCosineSample(state)));
    ray3 scatteredRay = ray3(record.pointHit, scatteredDir);

    record.didScatter = true;
    record.scatteredRay = scatteredRay;
}

// Lambertian BRDF 
vec3 lambertianBRDF(vec3 objectColor){
    return objectColor / PI;
}

// Scattering for reflective objects with the math given by https://raytracing.github.io/books/RayTracingInOneWeekend
void reflectiveScatter(float fuzz, ray3 ray, inout hitRecord record, inout uvec4 state){
    vec3 reflectDir = reflect(ray.direction, record.normalVector);
    reflectDir = normalize(reflectDir) + fuzz * randomVectorOnUnitSphere(state);
    ray3 scatteredRay = ray3(record.pointHit, reflectDir);

    if (dot(reflectDir, record.normalVector) > 0){
        record.didScatter = true;
    } else {
        record.didScatter = false;
    }
    record.scatteredRay = scatteredRay;
}

// Scattering for objects where both refraction and reflection is possible. Math given by https://raytracing.github.io/books/RayTracingInOneWeekend.html 
void dielectricScatter(float etaRatio, ray3 ray, inout hitRecord record, inout uvec4 state){
    if (record.frontFace){
        etaRatio = 1 / etaRatio;
    } 

    vec3 unitDirection = normalize(ray.direction);
    float cosTheta = min(dot(-unitDirection, record.normalVector), 1);
    float sinTheta = sqrt(1 - pow(cosTheta, 2));

    vec3 rayDir; 
    if (etaRatio * sinTheta > 1 || reflectance(cosTheta, etaRatio) > rand(state)){
        rayDir = reflect(unitDirection, record.normalVector);
    } else {
        rayDir = refract(unitDirection, record.normalVector, etaRatio);
    }
    
    record.didScatter = true; 
    record.scatteredRay = ray3(record.pointHit, rayDir);
}

// General scattering 
void scatter(int materialID, float materialParameter, ray3 ray, inout hitRecord record, inout uvec4 state){
    record.materialID = materialID;
    if (materialID == LAMBERTIAN){
        lambertianScatter(record, state);
    } else if (materialID == REFLECTIVE){
        reflectiveScatter(materialParameter, ray, record, state);
    } else if (materialID == DIELECTRIC){
        dielectricScatter(materialParameter, ray, record, state);
    } else if (materialID == LIGHT){
        if (!record.frontFace){
            record.objectColor = vec3(0);
        }
        record.isLight = true;
    }
}

